#
# Maintainer: Ste74 <capitani74atgmaildotcom>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-session-bzr-manjaro
pkgver=r63
pkgrel=2
pkgdesc='The Pantheon Session Handler'
arch=('any')
url='https://code.launchpad.net/~elementary-os/elementaryos/pantheon-xsession-settings'
license=('GPL3')
groups=('pantheon-unstable')
depends=('cerbere-bzr' 'gala-bzr-manjaro' 'gconf' 'gnome-keyring' 'gnome-session'
         'gnome-settings-daemon' 'gnome-user-share' 'polkit-gnome'
         'slingshot-launcher-bzr-manjaro' 'wingpanel-bzr-manjaro' 'xdg-user-dirs-gtk')
makedepends=('bzr')
optdepends=('pantheon-default-settings-bzr')
source=('pantheon-session.zip'
        'pantheon-session.sh')
sha256sums=('917a1b661199d45169c4fa1352bae1e3194becea4e8bd239e4071b09621d2616'
            '1e0c8e9403e52614d43ecb920e507ee1e96b1439bb20949ff814741e5e5e2284')

prepare() {
  cd pantheon-session

  sed 's/policykit-1/polkit/' -i autostart/polkit-gnome-authentication-agent-1-pantheon.desktop
  sed 's|gnome-session --session=pantheon|/usr/bin/pantheon-session|' -i debian/pantheon.desktop
}

package() {
  cd pantheon-session

  mkdir -p "${pkgdir}"/{etc/xdg,usr/{bin,share/{gnome-session/sessions,pantheon,xsessions}}}
  cp -dr --no-preserve='ownership' autostart "${pkgdir}/etc/xdg/"
  cp -dr --no-preserve='ownership' debian/pantheon.desktop "${pkgdir}/usr/share/xsessions/"
  cp -dr --no-preserve='ownership' debian/pantheon.session "${pkgdir}/usr/share/gnome-session/sessions/"
  cp -dr --no-preserve='ownership' gconf "${pkgdir}/usr/share/GConf"
  cp -dr --no-preserve='ownership' unity-greeter "${pkgdir}/usr/share/"
  cp -dr --no-preserve='ownership' applications "${pkgdir}/usr/share/pantheon/"

  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +

# gnome-session workaround
  install -m 755 ../pantheon-session.sh "${pkgdir}"/usr/bin/pantheon-session
}

# vim: ts=2 sw=2 et:
